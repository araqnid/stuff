apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = 'org.araqnid.stuff.Main'
sourceCompatibility = 1.7
version = '0.0.' + (System.getenv("BUILD_NUMBER") ?: "0")

jar {
    manifest {
        attributes 'Implementation-Title': 'Stuff',
                   'Implementation-Version': version,
		   'X-Service-Class': mainClassName
    }
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output;
        runtimeClasspath += main.output + test.output;
    }
    browserTest {
        compileClasspath += main.output;
        runtimeClasspath += main.output;
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    browserTestCompile.extendsFrom testCompile
    browserTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'com.google.inject:guice:3.0'
    compile 'com.google.inject.extensions:guice-servlet:3.0'
    compile 'com.google.inject.extensions:guice-multibindings:3.0'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.eclipse.jetty:jetty-server:9.2.3.v20140905'
    compile 'org.eclipse.jetty:jetty-servlet:9.2.3.v20140905'
    compile 'com.surftools:BeanstalkClient:1.4.6'
    compile 'redis.clients:jedis:2.5.2'
    //compile 'org.hibernate:hibernate-core:4.3.6.Final'
    //compile 'org.hibernate:hibernate-entitymanager:4.3.6.Final'
    compile 'org.jboss.resteasy:resteasy-jaxrs:3.0.8.Final'
    compile 'org.jboss.resteasy:resteasy-guice:3.0.8.Final'
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:3.0.8.Final'
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.4.2'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.2'
    compile 'com.fasterxml.jackson.module:jackson-module-guice:2.4.1'
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:2.4.2'
    compile 'com.lexicalscope.eventcast:eventcast:1.0.2'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    compile 'joda-time:joda-time:2.4'
    compile 'org.postgresql:postgresql:9.3-1102-jdbc41'
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    browserTestCompile ('org.seleniumhq.selenium:selenium-java:2.42.2') {
        exclude module: 'httpclient'
    }
}

task runtimeDeps(dependsOn: 'processResources') << {
    def metainf = new File("$buildDir/resources/main/META-INF");
    metainf.mkdirs()
    def depsFile = new File(metainf, "stuff.deps.txt");
    depsFile.text = ''
    configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        depsFile.text += artifact.moduleVersion.id.toString() + ' ' + artifact.type + '\n'
    }
}

jar.dependsOn(runtimeDeps)

task integrationTest(type: Test, dependsOn: 'test') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task browserTest(type: Test) {
    testClassesDir = sourceSets.browserTest.output.classesDir
    classpath = sourceSets.browserTest.runtimeClasspath
}

build.dependsOn(integrationTestClasses, browserTestClasses)
check.dependsOn(integrationTest)

eclipse {
    classpath {
        plusConfigurations += configurations.integrationTestCompile;
        plusConfigurations += configurations.browserTestCompile;
    }
}
