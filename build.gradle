apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'application'

mainClassName = 'org.araqnid.stuff.Main'
sourceCompatibility = 1.8
version = '0.0.' + (System.getenv("BUILD_NUMBER") ?: System.getenv("TRAVIS_BUILD_NUMBER") ?: "0")

jar {
    manifest {
        attributes 'Implementation-Title': project.description ?: project.name,
                   'Implementation-Version': version,
                   'X-Service-Class': mainClassName
    }
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output;
        runtimeClasspath += main.output + test.output;
    }
    browserTest {
        compileClasspath += main.output;
        runtimeClasspath += main.output;
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    browserTestCompile.extendsFrom testCompile
    browserTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

dependencies {
    def jettyVersion = '9.2.9.v20150224'
    def jacksonVersion = '2.5.1'
    def resteasyVersion = '3.0.10.Final'
    def hibernateVersion = '4.3.8.Final'

    compile 'com.google.guava:guava:18.0'
    compile 'com.google.inject:guice:3.0'
    compile 'com.google.inject.extensions:guice-servlet:3.0'
    compile 'com.google.inject.extensions:guice-multibindings:3.0'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.eclipse.jetty:jetty-server:' + jettyVersion
    compile 'org.eclipse.jetty:jetty-servlet:' + jettyVersion
    compile 'org.eclipse.jetty:apache-jsp:' + jettyVersion
    compile 'org.eclipse.jetty:apache-jstl:' + jettyVersion
    compile 'com.surftools:BeanstalkClient:1.4.6'
    compile 'redis.clients:jedis:2.6.0'
    compile 'com.google.code.findbugs:jsr305:3.0.0'
    compile 'org.hibernate:hibernate-core:' + hibernateVersion
    compile 'org.hibernate:hibernate-entitymanager:' + hibernateVersion
    compile 'org.jboss.resteasy:resteasy-jaxrs:' + resteasyVersion
    compile 'org.jboss.resteasy:resteasy-guice:' + resteasyVersion
    compile 'org.jboss.resteasy:resteasy-jackson2-provider:' + resteasyVersion
    compile 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:' + jacksonVersion
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-joda:' + jacksonVersion
    compile 'com.fasterxml.jackson.module:jackson-module-guice:' + jacksonVersion
    compile 'com.fasterxml.jackson.datatype:jackson-datatype-guava:' + jacksonVersion
    compile 'com.lexicalscope.eventcast:eventcast:1.0.2'
    compile 'joda-time:joda-time:2.7'
    compile 'org.postgresql:postgresql:9.3-1102-jdbc41'
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.hamcrest:hamcrest-integration:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    browserTestCompile ('org.seleniumhq.selenium:selenium-java:2.42.2') {
        exclude module: 'httpclient'
    }
    integrationTestCompile 'org.apache.httpcomponents:httpclient:4.3.5'
    // marking this as integrationTestCompile causes a conflict for Eclipse which tries to take the transitive 4.2 version via resteasy too
    compile 'org.apache.httpcomponents:httpclient:4.3.5'
}

task runtimeDeps(dependsOn: 'processResources') << {
    def sha1 = java.security.MessageDigest.getInstance("SHA-1");
    def metainf = new File("$buildDir/resources/main/META-INF");
    metainf.mkdirs()
    def depsFile = new File(metainf, project.name + ".deps.txt");
    depsFile.text = ''
    configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        depsFile.text += sha1.digest(artifact.file.bytes).collect {String.format "%02x", it}.join() + ' ' + artifact.moduleVersion.id + ' ' + artifact.type + '\n'
    }
}

jar.dependsOn(runtimeDeps)

task integrationTest(type: Test, dependsOn: 'test') {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task browserTest(type: Test) {
    testClassesDir = sourceSets.browserTest.output.classesDir
    classpath = sourceSets.browserTest.runtimeClasspath
}

build.dependsOn(integrationTestClasses, browserTestClasses)
check.dependsOn(integrationTest)

eclipse {
    classpath {
        plusConfigurations += configurations.integrationTestCompile;
        plusConfigurations += configurations.browserTestCompile;
    }
}
